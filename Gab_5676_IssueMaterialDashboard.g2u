Program.Sub.ScreenSU.Start
Gui.Form..Create
Gui.Form..Caption("Issue Material")
Gui.Form..Size(16515,11100)
Gui.Form..MinX(0)
Gui.Form..MinY(0)
Gui.Form..Position(0,0)
Gui.Form..BackColor(-2147483633)
Gui.Form..MousePointer(0)
Gui.Form..Event(UnLoad,Form_UnLoad)
Gui.Form.GsMain.Create(GsGridControl)
Gui.Form.GsMain.Size(16305,10050)
Gui.Form.GsMain.Position(75,585)
Gui.Form.GsMain.Event(RowCellClick,GsMain_RowCellClick)
Gui.Form.GsMain.Anchor(15)
Gui.Form.GsMain.Event(CellValueChanged,GsMain_CellValueChanged)
Gui.Form.proBar1.Create(ProgressBar)
Gui.Form.proBar1.Size(3735,120)
Gui.Form.proBar1.Position(75,390)
Gui.Form.lblStatus.Create(Label,"Label",True,6480,255,0,60,120,True,0,"Arial",8,-2147483633,0)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
V.Global.sRet.Declare
Program.Sub.Preflight.End

Program.Sub.Main.Start
'21 Mar 2024 - BixRB
'Added Finished Good Product Line from Job Header
f.Intrinsic.Control.Try
	v.Local.sError.Declare	

	F.ODBC.Connection!conx.OpenCompanyConnection(300)
	
	gui.Form.lblStatus.Caption("Loading...")
	F.Intrinsic.Control.CallSub("CreateContextMenus")
	
	F.Intrinsic.Control.CallSub("CreateTable")
	
'	gui.Form..Show
	
	F.Intrinsic.Control.CallSub("LoadDTJobCommitments")
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry	
Program.Sub.Main.End

Program.Sub.Refresh.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	
	F.Intrinsic.Control.CallSub(Serialize)
	F.Intrinsic.Control.CallSub("LoadDTJobCommitments")

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry	
Program.Sub.Refresh.End

Program.Sub.CreateTable.Start
	V.Local.bExists.Declare(boolean)
	
	F.ODBC.Connection!conx.TableExists("GAB_5676_NOTES", V.Local.bExists)
	F.Intrinsic.Control.If(V.Local.bExists, =, False)
		F.ODBC.Connection!conx.Execute("CREATE TABLE GAB_5676_NOTES (JOB CHAR(6), SUFFIX CHAR(3), SEQUENCE CHAR(6), NOTES CHAR(30), DATE_CREATED DATE);")
		F.ODBC.Connection!conx.Execute("CREATE INDEX GAB_5676_NOTES01 USING 0  ON GAB_5676_NOTES(JOB)")
		F.ODBC.Connection!conx.Execute("CREATE INDEX GAB_5676_NOTES02 USING 1  ON GAB_5676_NOTES(SUFFIX)")
		F.ODBC.Connection!conx.Execute("CREATE INDEX GAB_5676_NOTES03 USING 2  ON GAB_5676_NOTES(SEQUENCE)")
	F.Intrinsic.Control.EndIf	
Program.Sub.CreateTable.End

Program.Sub.LoadDTJobCommitments.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	v.Local.iCnt.Declare
	v.Local.sFilter.Declare
	v.Local.sPart.Declare
	v.Local.sLocation.Declare
	v.Local.fOnhand.Declare	
	
	gui.Form.lblStatus.Caption("Getting commitments...")
	F.Intrinsic.Control.If(V.DataTable.dtJobCommitments.Exists)
		F.Data.DataTable.Close("dtJobCommitments")
	F.Intrinsic.Control.EndIf	
	'F.Data.DataTable.CreateFromSQL("dtJobCommitments", "conx", "Select RTrim(A.Part) as Part,A.Location,B.Description,H.Part as MfgPart,H.Product_Line as MfgPL,H.Customer,H.Flag_Hold,H.Priority, A.Job, A.Suffix, N.Notes as Notes, A.Sequence, (A.Qty_Committed - A.Qty_Issued) as QtyRequired, B.Qty_OnHand, A.Date_Due,(B.Qty_OnOrder_Wo + B.Qty_OnOrder_PO) as QtyOnOrder, B.Qty_Required as Total_Required, RTRIM(H.Customer_PO) AS Customer_PO, RTRIM(H.Comments_1) AS Comments_1, RTRIM(H.Comments_2) AS Comments_2 from V_JOB_COMMITMENTS A left join V_Inventory_Mstr B on A.Part = B.Part and A.Location = B.Location left join V_Job_Header H on A.Job = H.Job and A.Suffix = H.Suffix left join GAB_5676_NOTES N on A.Job = N.Job and A.Suffix = N.Suffix and A.Sequence = N.Sequence", True)	
	'Add Bin to dashboard - 2/18/2021 ALW
	F.Data.DataTable.CreateFromSQL("dtJobCommitments", "conx", "select B.Bin as Bin, RTrim(A.Part) as Part,A.Location,B.PRODUCT_LINE as FGPL,B.Description,H.Part as MfgPart,H.Product_Line as MfgPL,H.Customer,H.Flag_Hold,H.Priority, A.Job, A.Suffix, N.Notes as Notes, A.Sequence, (A.Qty_Committed - A.Qty_Issued) as QtyRequired, B.Qty_OnHand, A.Date_Due,(B.Qty_OnOrder_Wo + B.Qty_OnOrder_PO) as QtyOnOrder, B.Qty_Required as Total_Required, RTRIM(H.Customer_PO) AS Customer_PO, RTRIM(H.Comments_1) AS Comments_1, RTRIM(H.Comments_2) AS Comments_2 from V_JOB_COMMITMENTS A left join V_Inventory_Mstr B on A.Part = B.Part and A.Location = B.Location left join V_Job_Header H on A.Job = H.Job and A.Suffix = H.Suffix left join GAB_5676_NOTES N on A.Job = N.Job and A.Suffix = N.Suffix and A.Sequence = N.Sequence", True)	
	f.Data.DataTable.AddColumn("dtJobCommitments","RunningTotal","Float",0)
	
	'Clone
	f.Intrinsic.Control.If(v.DataTable.dtJCMT.Exists)
		f.Data.DataTable.Close("dtJCMT")
	f.Intrinsic.Control.EndIf
	f.Data.DataTable.Clone("dtJobCommitments","dtJCMT",True)	
	f.Data.DataTable.AddColumn("dtJCMT","Ready","String","")	
	
	'Get Distinct Part
	gui.Form.lblStatus.Caption("Getting distinct Parts...")
	f.Data.DataView.Create("dtJobCommitments","dvJobCommitments")
	f.Intrinsic.Control.If(v.DataTable.Parts.Exists)
		f.Data.DataTable.Close("Parts")
	f.Intrinsic.Control.EndIf
	f.Data.DataView.ToDataTableDistinct("dtJobCommitments","dvJobCommitments","Parts","Part*!*Location",True)
	f.Data.DataView.Create("Parts","dvParts")
	
	'Turn on indicator
	f.Intrinsic.Control.If(v.DataTable.dtJobCommitments.RowCount,>,0)
		gui.Form.proBar1.Max(v.DataTable.Parts.RowCount)
		gui.Form.proBar1.Visible(True)
	f.Intrinsic.Control.EndIf	
	
	'Loop each Part in the distinct Part,Location list and do the MRP calculation
	gui.Form.lblStatus.Caption("MRP calculation...")
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.DataTable.Parts.RowCount--,1)
		gui.Form.proBar1.Value(v.Local.iCnt)
		v.Local.sPart.Set(v.DataTable.Parts(v.Local.iCnt).Part!FieldValString)
		v.Local.sLocation.Set(v.DataTable.Parts(v.Local.iCnt).Location!FieldValString)
		f.Intrinsic.String.Build("Part = '{0}' and Location = '{1}'",v.Local.sPart.PSQLFriendly,v.Local.sLocation,v.Local.sFilter)
		f.Data.DataView.SetFilter("dtJobCommitments","dvJobCommitments",v.Local.sFilter)
		f.Data.DataView.SetSort("dtJobCommitments","dvJobCommitments","Date_Due asc,Job,Suffix,Sequence")
		f.Data.DataView.ToDataTable("dtJobCommitments","dvJobCommitments","dtTemp",True)
		'Running Total Requirement
		v.Local.fOnhand.Set(v.DataTable.dtTemp(0).Qty_Onhand!FieldValString)
		f.Data.DataTable.RunningTotal("dtTemp","QtyRequired",v.Local.fOnhand,"-","RunningTotal")
		f.Data.DataTable.AddExpressionColumn("dtTemp","Ready","String","IIF([RunningTotal] >= 0,'Y','')")
		
		f.Data.DataTable.Merge("dtTemp","dtJCMT",True,3)
		f.Data.DataTable.Close("dtTemp")
		
	f.Intrinsic.Control.Next(v.Local.iCnt)
	gui.Form.proBar1.Visible(False)
	
	f.Data.DataTable.Close("dtJobCommitments")
	F.Data.DataView.Create("dtJCMT","dvJCMT",22,"","Date_Due ASC, Job ASC, Suffix ASC")
	f.Data.DataView.ToDataTable("dtJCMT","dvJCMT","dtJCMT2",True)
	f.Data.DataTable.Close("dtJCMT")
	f.Data.DataView.Create("dtJCMT2","dvJCMT2")
	f.Data.DataView.ToDataTable("dtJCMT2","dvJCMT2","dtJCMT",True)
	f.Data.DataTable.Close("dtJCMT2")
	
	gui.Form.lblStatus.Caption("Formatting grid...")
	F.Intrinsic.Control.CallSub("LoadGVJobCommitments")
	gui.Form.lblStatus.Caption("")
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry	
Program.Sub.LoadDTJobCommitments.End

Program.Sub.LoadGVJobCommitments.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare
	
	Gui.Form.GsMain.AddGridviewFromDatatable("gvJobCommitments", "dtJCMT")
	
	Gui.Form.GsMain.SuspendLayout()
	
	'General grid format
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","AllowSort",True)
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","AllowFilter",True)
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","OptionsFindAlwaysVisible",True)
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","EnableAppearanceOddRow",False)
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","OptionsViewShowAutoFilterRow",True)
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","OptionsViewColumnAutoWidth",False)	
	Gui.Form.GsMain.SetGridViewProperty("gvJobCommitments","ColumnPanelRowHeight","35")		
	'Set color based on Ready
	gui.Form.GsMain.AddStyleFormatCondition("gvJobCommitments","Ready","tagReady","equal","Y")
	gui.Form.GsMain.SetStyleFormatConditionProperty("gvJobCommitments","Ready","tagReady","backcolor","LightGreen")		
	'CellForeColor
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Part","CellForeColor",V.Enum.ThemeColors!ColorBlue)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job","CellForeColor",V.Enum.ThemeColors!ColorBlue)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Part","CellFontUnderline",True)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job","CellFontUnderline",True)	
	'Width
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Part","MinWidth","160")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Location","MinWidth","25")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","FGPL","MinWidth","30")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Description", "MinWidth","200")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPart", "MinWidth","160")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPL", "MinWidth","30")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Flag_Hold", "MinWidth","30")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Priority", "MinWidth","30")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Customer", "MinWidth","60")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job", "MinWidth","50")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Suffix", "MinWidth","50")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Sequence", "MinWidth","60")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Notes", "MinWidth","100")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","QtyRequired", "MinWidth","90")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Qty_OnHand", "MinWidth","90")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","QtyOnOrder", "MinWidth","90")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Total_Required", "MinWidth","90")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","RunningTotal", "MinWidth","90")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Ready", "MinWidth","45")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Customer_PO", "MinWidth","45")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Comments_1", "MinWidth","45")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Comments_2", "MinWidth","45")
	'HeaderHAlignment
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Location", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","FGPL", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPL","HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Flag_Hold","HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Priority", "HeaderHAlignment","center")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Suffix", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Sequence", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Customer", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Date_Due", "HeaderHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","QtyRequired", "HeaderHAlignment","far")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Qty_OnHand", "HeaderHAlignment","far")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","QtyOnOrder", "HeaderHAlignment","far")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Total_Required", "HeaderHAlignment","far")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","RunningTotal", "HeaderHAlignment","far")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Ready", "HeaderHAlignment","Center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Customer_PO", "HeaderHAlignment","Center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Comments_1", "HeaderHAlignment","Center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Comments_2", "HeaderHAlignment","Center")
	'Cell Alignment
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","FGPL","CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPL","CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Flag_Hold","CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Priority", "CellHAlignment","center")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Location", "CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job", "CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Suffix", "CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Sequence", "CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Customer", "CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Date_Due", "CellHAlignment","center")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Ready", "CellHAlignment","center")	
	'For Rowcell Click
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Part","AllowEdit",False)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Part","ReadOnly",True)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job","AllowEdit",False)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Job","ReadOnly",True)	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Notes","AllowEdit",True)
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Notes","ReadOnly",False)	
	'Caption
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","QtyRequired","Caption", "Qty Required")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","QtyOnOrder","Caption", "Qty On Order")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","RunningTotal","Caption", "Running Total")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Total_Required","Caption", "Total Required")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Qty_Onhand","Caption", "On Hand")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Date_Due","Caption", "Due Date")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","FGPL","Caption", "PL")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Location","Caption", "Locn")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Sequence","Caption", "Seq")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPart","Caption", "Mfg Part")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPL","Caption", "Mfg PL")	
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Flag_Hold","Caption", "Hold")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Customer_PO","Caption", "Customer PO")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Comments_1","Caption", "Comments 1")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Comments_2","Caption", "Comments 2")
	'Fixed left
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Part","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Location","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","FGPL","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Description","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPart","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","MfgPL","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Flag_Hold","Fixed","Left")
	Gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Priority","Fixed","Left")	

	gui.Form.GsMain.SetColumnProperty("gvJobCommitments","Date_Due","DisplayCustomDateTime","d")
	
	Gui.Form.GsMain.ResumeLayout()
	
	
	Gui.Form.GsMain.MainView("gvJobCommitments")
	F.Intrinsic.Control.CallSub(Deserialize)
	gui.Form..Show
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry	
Program.Sub.LoadGVJobCommitments.End

Program.Sub.GsMain_RowCellClick.Start
f.Intrinsic.Control.Try	
	v.Local.sError.Declare
	
	F.Intrinsic.Control.If(V.Args.Column, =, "Part")
		F.Intrinsic.String.Concat(V.DataTable.dtJCMT(V.Args.RowIndex).Part!FieldVal, "!*! ", V.Global.sRet)
		F.Global.General.CallWrapperAsync(300010, V.Global.sRet)
	F.Intrinsic.Control.ElseIf(V.Args.Column, =, "Job")
		F.Intrinsic.String.Build("{0}!*!{1}!*! !*! !*! !*! !*! ", V.DataTable.dtJCMT(V.Args.RowIndex).Job!FieldVal, V.DataTable.dtJCMT(V.Args.RowIndex).Suffix!FieldVal,V.Global.sRet)
		F.Global.General.CallWrapperAsync(450150, V.Global.sRet)
	F.Intrinsic.Control.EndIf
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry		
Program.Sub.GsMain_RowCellClick.End

Program.Sub.CreateContextMenus.Start
f.Intrinsic.Control.Try	
	v.Local.sError.Declare
	
	Gui.Form..ContextMenuCreate("ctxJobCommitments")
	Gui.Form.GsMain.ContextMenuAttach("ctxJobCommitments")
	
	Gui.Form..ContextMenuAddItem("ctxJobCommitments","Export",0,"Export")	
	Gui.Form..ContextMenuAddItem("ctxJobCommitments","Refresh",0,"Refresh")	
	Gui.Form..ContextMenuAddItem("ctxJobCommitments","Clear Sorting",0,"Clear Sorting")
	Gui.Form..ContextMenuAddItem("ctxJobCommitments","Reset",0,"Reset Grid Formatting")
	
	Gui.Form..ContextMenuSetItemEventHandler("ctxJobCommitments","Export","MenuClickExport")
	Gui.Form..ContextMenuSetItemEventHandler("ctxJobCommitments","Refresh","Refresh")	
	Gui.Form..ContextMenuSetItemEventHandler("ctxJobCommitments","Clear Sorting","ClearSorting")
	Gui.Form..ContextMenuSetItemEventHandler("ctxJobCommitments","Reset","ResetFormatting")
	
F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry	
Program.Sub.CreateContextMenus.End

Program.Sub.MenuClickExport.Start
	F.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	V.Local.sFileExport.Declare
	V.Local.bExcel.Declare
	V.Local.bFileLocked.Declare
	V.Local.sMsg.Declare
	V.Local.sForm.Declare(String,"Form")
	V.Local.sControl.Declare(String, "GsMain")
	V.Local.bChart.Declare
	
	F.Intrinsic.Control.If(V.Local.bChart,=,False)
		F.Automation.MSExcel.CheckPresence(V.Local.bExcel)
		F.Intrinsic.Control.If(V.Local.bExcel)
			F.Intrinsic.String.Build("{0}\GAB_5676_IssueMaterialDashboard.xlsx",V.Caller.LocalGssTempDir,V.Local.sFileExport)
			F.Intrinsic.File.IsFileLocked(V.Local.sFileExport,V.Local.bFileLocked)
			F.Intrinsic.Control.If(V.Local.bFileLocked)
				F.Intrinsic.String.Build("File is already open. Please close and export again.{0}File: {1}",V.Ambient.NewLine,V.Local.sFileExport,V.Local.sMsg)
				F.Intrinsic.UI.Msgbox(V.Local.sMsg,"File In Use")
			F.Intrinsic.Control.Else
				Gui.Form.GsMain.Export(V.Local.sFileExport,"xlsx")
				F.Intrinsic.Task.ShellExec(0,"",V.Local.sFileExport,"","",1)
			F.Intrinsic.Control.EndIf
		F.Intrinsic.Control.Else
			F.Intrinsic.String.Build("{0}\GAB_5676_IssueMaterialDashboard.csv",V.Caller.LocalGssTempDir,V.Local.sFileExport)
			F.Intrinsic.File.IsFileLocked(V.Local.sFileExport,V.Local.bFileLocked)
			F.Intrinsic.Control.If(V.Local.bFileLocked)
				F.Intrinsic.String.Build("File is already open. Please close and export again.{0}File: {1}",V.Ambient.NewLine,V.Local.sFileExport,V.Local.sMsg)
				F.Intrinsic.UI.Msgbox(V.Local.sMsg,"File In Use")
			F.Intrinsic.Control.Else
				Gui.Form.GsMain.Export(V.Local.sFileExport,"csv")
			F.Intrinsic.Control.EndIf
			F.Intrinsic.Task.ShellExec(0,"",V.Local.sFileExport,"","",1)
		F.Intrinsic.Control.EndIf
	F.Intrinsic.Control.Else
		F.Intrinsic.String.Build("{0}\{1}.jpg",V.Caller.LocalGSSTempDir,V.Local.sControl,V.Local.sFileExport)
		Gui.[V.Local.sForm].[V.Local.sControl].ImageToFile("JPEG",V.Local.sFileExport)
		F.Intrinsic.Task.ShellExec(0,"Open",V.Local.sFileExport,"","",0)
	F.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.Catch
		F.Intrinsic.String.Build("Project: {0}{1}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}{1}GAB Version: {5}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
		F.Intrinsic.UI.Msgbox(V.Local.sError)
		F.Intrinsic.Control.CallSub("Unload")
	F.Intrinsic.Control.EndTry
Program.Sub.MenuClickExport.End

Program.Sub.ClearSorting.Start
Gui.Form.GsMain.ClearSorting("gvJobCommitments")
Program.Sub.ClearSorting.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.CallSub("Unload")
Program.Sub.Form_UnLoad.End

Program.Sub.UnLoad.Start
f.Intrinsic.Control.Try
	v.Local.sError.Declare

	F.Intrinsic.Control.CallSub(Serialize)
	F.Intrinsic.Control.If(V.ODBC.conx.State,=,1)
		F.ODBC.Connection!conx.close
	F.Intrinsic.Control.EndIf
	
	f.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	f.Intrinsic.String.Build("Subroutine Error {0}{0}Subroutine: {1}{0}Error Occurred {2} with description {3}",V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	F.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.UnLoad.End

Program.Sub.GsMain_CellValueChanged.Start

V.Local.sJob.Declare
V.Local.sSuffix.Declare
V.Local.sSequence.Declare
V.Local.sTextTbl.Declare
V.Local.sSql.Declare
V.Local.sDate.Declare

F.Intrinsic.String.Format(V.Ambient.Date, "yyyy-mm-dd", V.Local.sDate)
	
f.Intrinsic.Control.SelectCase(V.Args.column)
	F.Intrinsic.Control.Case("Notes")
		Gui.Form.GsMain.GetCellValueByColumnName("gvJobCommitments","Job",V.Args.RowIndex,V.Local.sJob)
		Gui.Form.GsMain.GetCellValueByColumnName("gvJobCommitments","Suffix",V.Args.RowIndex,V.Local.sSuffix)
		Gui.Form.GsMain.GetCellValueByColumnName("gvJobCommitments","Sequence",V.Args.RowIndex,V.Local.sSequence)
		F.Intrinsic.String.Replace(V.Args.Value,"'","''",V.Local.sTextTbl)
		F.Intrinsic.String.Build("SELECT * From GAB_5676_NOTES WHERE JOB = '{0}' AND SUFFIX = '{1}' AND SEQUENCE = '{2}';",V.Local.sJob,V.Local.sSuffix,V.Local.sSequence,V.Local.sSql)
		F.ODBC.Connection!conx.OpenRecordsetRW("rstNotes",V.Local.sSQL)
		F.Intrinsic.Control.If(V.ODBC.conx!rstNotes.EOF,=,True)
			F.Intrinsic.String.Build("INSERT INTO GAB_5676_NOTES(JOB,SUFFIX,SEQUENCE,NOTES,DATE_CREATED) VALUES('{0}','{1}','{2}','{3}','{4}');",V.Local.sJob,V.Local.sSuffix,V.Local.sSequence,V.Local.sTextTbl,V.Local.sDate,V.Local.sSQL)
			F.ODBC.Connection!conx.Execute(V.Local.sSQL)
		F.Intrinsic.Control.Else
			F.Intrinsic.String.Build("UPDATE GAB_5676_NOTES SET NOTES = '{0}', DATE_CREATED ='{1}' WHERE JOB='{2}' AND SUFFIX ='{3}' AND SEQUENCE ='{4}';",V.Local.sTextTbl,V.Local.sDate,V.Local.sJob,V.Local.sSuffix,V.Local.sSequence,V.Local.sSQL)
			F.ODBC.Connection!conx.Execute(V.Local.sSQL)
		F.Intrinsic.Control.EndIf
		F.ODBC.conx!rstNotes.Close
f.Intrinsic.Control.EndSelect

Program.Sub.GsMain_CellValueChanged.End

Program.Sub.Serialize.Start

V.Local.sSerialize.Declare

Gui.Form.GsMain.Serialize("gvJobCommitments",V.Local.sSerialize)

F.Global.Registry.AddValue(V.Caller.User,V.Caller.CompanyCode,"gvJobCommitments",100501,5676,False,"Serialize",False,0,0,V.Ambient.Date,V.Ambient.Time,V.Local.sSerialize)

Program.Sub.Serialize.End

Program.Sub.Deserialize.Start

V.Local.sSerialize.Declare

F.Global.Registry.ReadValue(V.Caller.User,V.Caller.CompanyCode,"gvJobCommitments",100501,5676,6,"",V.Local.sSerialize)
F.Intrinsic.Control.If(V.Local.sSerialize.Trim,<>,"")
	Gui.Form.GsMain.Deserialize(V.Local.sSerialize)
F.Intrinsic.Control.EndIf

Program.Sub.Deserialize.End

Program.Sub.ResetFormatting.Start

F.Global.Registry.AddValue(V.Caller.User,V.Caller.CompanyCode,"gvJobCommitments",100501,5676,False,"Serialize",False,0,-999.0,1/1/1980,12:00:00 AM,"")
Gui.Form.GsMain.Visible(False)
Gui.Form.GsMain.SuspendLayout()
F.Intrinsic.Control.CallSub(LoadDTJobCommitments)
Gui.Form.GsMain.ResumeLayout()
Gui.Form.GsMain.Visible(True)

Program.Sub.ResetFormatting.End

Program.Sub.Comments.Start
${$0$}$$}$$}$12:00:00 AM$}$False
${$3$}$0$}$$}$0$}$-1$}$$}$12:00:00 AM$}$ShippingDashPickList.rpt report required
${$5$}$20.1.8819.26349$}$1
${$6$}$bbaker$}$20240321151334248$}$pM7xsHplA2E9VCTenDTJ47gi1rF/t66W0Wa0CFH+k+K3zgbJz9KfwXt0If99oQilwG7Y58Cmg5Q=
Program.Sub.Comments.End

